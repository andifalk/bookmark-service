
plugins {
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java'
    id 'jacoco'
    id 'com.github.spotbugs' version '5.0.13'
    id 'org.owasp.dependencycheck' version '7.3.2'
    id 'org.sonarqube' version '3.5.0.2730'
}

group = 'com.example'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.passay:passay:1.6.2'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.13'
    implementation 'org.springdoc:springdoc-openapi-security:1.6.13'
    implementation 'com.google.common.html.types:types:1.0.8'
    implementation 'org.apache.tika:tika-parsers:1.28.3'
    implementation 'commons-fileupload:commons-fileupload:1.4'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.github.hakky54:logcaptor:2.7.10'
    testImplementation 'com.tngtech.archunit:archunit-junit5-engine:0.23.1'
    spotbugs 'com.github.spotbugs:spotbugs:4.7.3'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
    testImplementation 'com.github.spotbugs:spotbugs-annotations:4.7.3'
    implementation 'org.apache.poi:poi:5.2.2'
    implementation 'org.apache.poi:poi-ooxml:5.2.2'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

spotbugsMain {
    reports{
        xml {
            enabled = false
        }
        html {
            enabled = true
        }
    }
}

dependencyCheck {
    format = 'ALL'
    analyzers {
        msbuildEnabled = false
        assemblyEnabled = false
        nuspecEnabled = false
        composerEnabled = false
        swiftEnabled = false
        nodeEnabled = false
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination layout.buildDirectory.dir('jacocoHtml').get().asFile
    }
}